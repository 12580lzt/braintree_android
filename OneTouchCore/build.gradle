import groovy.json.JsonOutput
import groovy.json.JsonSlurper

apply plugin: 'com.android.library'

def getLatestSha1 = { ->
    "git log -1 --pretty=format:%H".execute().text.trim()
}

def getBuildVersion = { ->
    "git describe --match=*[0-9]*.[0-9]*.*[0-9] --tags --dirty --always".execute().text.trim()
}

ext {
    latest_sha1 = getLatestSha1()
    product_name = "OneTouchCore-Android"

    jar_name = product_name.replaceAll("-", "")
    product_version = getBuildVersion()

    build_time = new Date().format("MM/dd/yyyy HH:mm:ss Z")

    configFileText = new File("${projectDir}/otc-config.android.json").getText('UTF-8')
    configFile = JsonOutput.toJson(new JsonSlurper().parseText(configFileText)).replaceAll("\"", "\\\\\"")
}

// TODO rename this file to indicate it's just dyson
def paypalVersion = 'OneTouchCoreAndroid-2.4.0-6-g2f87477.jar'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 23

        buildConfigField "String", "PRODUCT_NAME", "\"${product_name}\""
        buildConfigField "String", "PRODUCT_VERSION", "\"${product_version}\""
        buildConfigField "String", "PRODUCT_FEATURES", "\"\""
        buildConfigField "String", "BUILD_TIME", "\"${build_time}\""
        buildConfigField "String", "LATEST_SHA1", "\"${latest_sha1}\""
        buildConfigField "String", "CONFIG_FILE", "\"${configFile}\""

    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile files('libs/' + paypalVersion)
    compile 'com.squareup.okhttp:okhttp:2.5.0'
}