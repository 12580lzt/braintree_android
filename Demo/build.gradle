apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.braintreepayments.demo"
        minSdkVersion 18
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    signingConfigs {
        debug {
            storeFile file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
        release {
            storeFile file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            signingConfig signingConfigs.release
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.pro'
        }
    }

    lintOptions {
        warning 'InvalidPackage'
        textReport true
        textOutput 'stdout'
    }

    packagingOptions {
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
    }
}

repositories {
    maven {
        url "https://jitpack.io"
    }
}

configurations.all {
    resolutionStrategy.force 'com.android.support:support-annotations:23.0.1'
}

dependencies {
    compile 'com.android.support:support-v4:24.2.1'

    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.lukekorth:mailable_log:0.1.3'

    compile 'com.google.android.gms:play-services-wallet:9.6.1'

    compile project(':Braintree')
    compile 'io.card:android-sdk:5.4.0'
    compile('com.braintreepayments.api:drop-in:3.0.0') {
        exclude group: 'com.braintreepayments.api', module: 'braintree'
    }

    debugCompile 'com.facebook.stetho:stetho:1.4.1'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4'

    androidTestCompile project(path: ':TestUtils', configuration: 'instrumentationTestDebug')
    testCompile project(path: ':TestUtils', configuration: 'unitTestDebug')
    androidTestCompile 'com.github.lkorth:device-automator:02fa2d56d9'
}

task demoApkSize<<{
  def apkDir = fileTree('./build/outputs/apk')
  def apks = apkDir.matching {
    include '*.apk'
    exclude '*unaligned*'
  }
  apks.visit { apk ->
    def apkName = apk.getName()
    def mb = apk.getSize() / 1024 / 1000
    mb = mb.setScale(2, BigDecimal.ROUND_UP)
    println "Size of ${apkName} is ${mb} MB"
  }
}
demoApkSize.group = 'Braintree'
demoApkSize.description = 'Prints the current size of the Demo apk'

project.tasks.findAll {
  if (it.name.startsWith('assemble')) {
    it.finalizedBy demoApkSize
  }
}
