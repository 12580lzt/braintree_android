apply plugin: 'com.android.library'

def DEVELOPMENT_URL = System.properties['DEVELOPMENT_URL'] ?: '"http://10.0.2.2:3000/"';

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        consumerProguardFiles 'proguard.pro'

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    testOptions {
        unitTests {
            all {
                jvmArgs '-noverify'
            }
        }
    }

    packagingOptions {
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
    }

    sourceSets {
        String sharedTestDir = 'src/sharedTest/java'
        androidTest {
            java.srcDir sharedTestDir
        }
        test {
            java.srcDir sharedTestDir
        }
    }
}

dependencies {
    compile 'com.google.android.gms:play-services-wallet:11.4.0'
    compile 'com.android.support:support-annotations:26.0.0'

    compile project(':Core')
    compile project(':BraintreeDataCollector')
    compile project(':PayPalOneTouch')
    compile 'com.braintreepayments.api:visa-checkout:0.1.1'

    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.2'
    androidTestCompile 'com.android.support.test:rules:1.0.0'
    androidTestCompile 'com.android.support.test:runner:1.0.0'
    androidTestCompile project(':TestUtils')

    testCompile 'org.robolectric:robolectric:3.5.1'
    testCompile 'org.powermock:powermock-module-junit4:1.6.6'
    testCompile 'org.powermock:powermock-module-junit4-rule:1.6.6'
    testCompile 'org.powermock:powermock-api-mockito:1.6.6'
    testCompile 'org.powermock:powermock-classloading-xstream:1.6.6'
    testCompile 'org.skyscreamer:jsonassert:1.4.0'
    testCompile project(':TestUtils')
}

android.buildTypes.each { type ->
    type.buildConfigField 'String', 'DEVELOPMENT_URL', DEVELOPMENT_URL
}

task runAllTests {
    /*
     * Empty task to run via command line Rakefile or ci.sh to indicate that tests that depend on
     * setting up external services (like the test invalid https server) are set up correctly.
     * Otherwise, these tests will be skipped
     */
}

gradle.taskGraph.whenReady { taskGraph ->
    def RUN_ALL_TESTS = taskGraph.hasTask(':Braintree:runAllTests')
    android.buildTypes.debug.buildConfigField('boolean', 'RUN_ALL_TESTS', '' + RUN_ALL_TESTS)
}

/* maven deploy + signing */
task javadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}

task javadocsJar(type: Jar, dependsOn: javadocs) {
    classifier = 'javadoc'
    from javadocs.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives javadocsJar
    archives sourcesJar
}

apply plugin: 'io.codearte.nexus-staging'
nexusStaging {
    packageGroup = "com.braintreepayments"
}

group = 'com.braintreepayments.api'
uploadArchives {
    repositories {
        mavenDeployer {
            pom.setArtifactId 'braintree'

            pom.project {
                name 'braintree'
                groupId = 'com.braintreepayments.api'
                version = "${android.defaultConfig.versionName}"
                packaging 'jar'
                description 'This is Braintree\'s Android SDK. Integrate this library to accept payments in your Android app.'
            }

            pom.whenConfigured { MavenPom pom ->
                def ignoredDependencies = [
                        'com.braintreepayments.api:data-collector',
                        'com.android.support:support-v4',
                        'com.android.support:appcompat-v7',
                        'com.android.support:design',
                        'com.google.android.gms:play-services-wallet',
                        'com.android.support:support-annotations',
                        'com.android.databinding:library',
                        'com.android.databinding:baseLibrary',
                        'com.android.databinding:adapters'
                ]
                pom.dependencies.each { dependency ->
                    def dependencyNotation = dependency.groupId + ':' + dependency.artifactId
                    dependency.optional = ignoredDependencies.contains(dependencyNotation)

                    logger.warn(dependency.toString() + ' optional? ' + dependency.optional)
                }
            }
        }
    }
}
