apply plugin: 'com.android.library'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        consumerProguardFiles 'proguard.pro'

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }
}

dependencies {
    // TODO This aar needs to be hosted in a centralized location such as Maven
    api(name: 'cardinal-sdk', version: '2.1.2', ext: 'aar')
    implementation group: 'com.nimbusds', name: 'nimbus-jose-jwt', version: '4.39'
    implementation group: 'com.madgag.spongycastle', name: 'core', version: '1.58.0.0'
    implementation group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.59'
}

/* maven deploy + signing */
task javadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}

task javadocsJar(type: Jar, dependsOn: javadocs) {
    classifier = 'javadoc'
    from javadocs.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives javadocsJar
    archives sourcesJar
}

apply plugin: 'io.codearte.nexus-staging'
nexusStaging {
    packageGroup = "com.braintreepayments"
}

uploadArchives {
    repositories {
        mavenDeployer {
            pom.setArtifactId 'three-d-secure'

            pom.project {
                name 'three-d-secure'
                groupId = 'com.braintreepayments.api'
                version = "${android.defaultConfig.versionName}"
                description '3DS dependencies for Braintree Android.'
            }
        }
    }
}
