apply plugin: 'com.android.library'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    compile 'com.android.support:support-annotations:[23.0.0,24.0.0)'

    compile files('libs/OneTouchCoreAndroid-3.0.0.jar')

    androidTestCompile project(':TestUtils')
}

/* maven deploy + signing */
task javadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocsJar(type: Jar, dependsOn: javadocs) {
    classifier = 'javadoc'
    from javadocs.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task releaseJar(type: Jar, dependsOn: android.libraryVariants.release.javaCompile) {
    from projectDir.path + '/build/intermediates/classes/release/'
    exclude { details -> details.file.name.endsWith('.xml') }

    from {
        configurations.compile.findAll {
            it.getName() != 'android.jar'
        }.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

artifacts {
    archives javadocsJar
    archives sourcesJar
    archives releaseJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            pom.setArtifactId 'data-collector'

            pom.project {
                name 'data-collector'
                groupId = 'com.paypal.android.sdk'
                version = "${android.defaultConfig.versionName}"
                packaging 'jar'
                description 'Fraud tools for PayPal integrations on Android.'
            }

            pom.whenConfigured { MavenPom pom ->
                pom.dependencies.each {
                    if (it.artifactId == 'support-annotations') {
                        it.optional = true
                    }
                }
            }
        }
    }
}
